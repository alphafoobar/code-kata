plugins {
    id "com.github.spotbugs" version "1.6.1"
}

version = '0.0.1-SNAPSHOT'

allprojects {
    apply plugin: 'java'
    apply plugin: 'jacoco'
    apply plugin: 'checkstyle'
    apply plugin: 'com.github.spotbugs'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    compileJava.options.encoding = "UTF-8"
    compileTestJava.options.encoding = "UTF-8"

    repositories {
        mavenCentral()
    }

    dependencies {
        implementation group: 'com.google.guava', name: 'guava', version: '23.0'
        implementation group: 'com.github.spotbugs', name: 'spotbugs-annotations', version: '3.1.6'

        testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.2.0'
        testImplementation group: 'org.mockito', name: 'mockito-core', version: '2.21.0'
        testImplementation group: 'org.assertj', name: 'assertj-core', version: '3.11.0'

        testImplementation group: 'com.google.guava', name: 'guava-testlib', version: '23.0'
        // Remove once Mockito 3.x gets released with Junit 5 support
        testImplementation group: 'org.mockito', name: 'mockito-junit-jupiter', version: '2.21.0'
        testRuntime group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.2.0'
    }

    spotbugs {
        toolVersion = '3.1.6'
    }

    tasks.withType(com.github.spotbugs.SpotBugsTask) {
        reports {
            xml.enabled = false
            html.enabled = true
            html.stylesheet resources.text.fromFile('../config/spotbugs/plain.xsl')
        }
    }

    checkstyle {
        configDir rootProject.file('config/checkstyle')
        configFile rootProject.file('config/checkstyle/checkstyle.xml')
        toolVersion = "7.6.1"
    }
}

task codeCoverageReport(type: JacocoReport) {
    // Gather execution data from all subprojects
    // (change this if you e.g. want to calculate unit test/integration test coverage separately)
    executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")

    // Add all relevant sourcesets from the subprojects
    subprojects.each {
        sourceSets it.sourceSets.main
    }

    reports {
        xml.enabled = true
        xml.destination new File("${buildDir}/reports/jacoco/report.xml")
        html.enabled = false
        csv.enabled = false
    }
}

//noinspection GroovyAssignabilityCheck
codeCoverageReport.dependsOn {
    subprojects*.test
}

check.dependsOn codeCoverageReport
